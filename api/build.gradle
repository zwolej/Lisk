buildscript {
    ext {
        gradleGitVersion = '1.7.2'
        gradleDockerVersion = '0.17.2'
        springBootVersion = '1.5.9.RELEASE'
        mariaDbVersion = '2.2.1'
        liquibaseVersion = '3.4.1'
        lombokVersion = '1.16.18'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:$gradleDockerVersion")
        classpath("org.ajoberstar:gradle-git:$gradleGitVersion")
        classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")
        classpath("org.springframework.cloud:spring-cloud-contract-gradle-plugin:1.2.1.RELEASE")
    }
}

group = 'io.lisk'
version = '1.1'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"
apply plugin: 'spring-cloud-contract'
apply plugin: 'com.palantir.docker'
apply plugin: 'maven-publish'

jar {
    baseName = "${project.name}"
    version =  "${version}"
}

repositories {
    mavenCentral()
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-contract-dependencies:1.2.1.RELEASE'
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.mariadb.jdbc:mariadb-java-client:$mariaDbVersion")
    compile("org.liquibase:liquibase-core:$liquibaseVersion")

    compileOnly("org.projectlombok:lombok:$lombokVersion")

    testCompile("org.springframework.cloud:spring-cloud-starter-contract-verifier")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("io.rest-assured:rest-assured:3.0.6")
}

contracts {
    baseClassForTests = 'io.lisk.contracts.MvcTest'
}

docker {
    name "io.lisk/${jar.baseName}:${grgit.head().getAbbreviatedId()}"
    dockerfile file('Dockerfile')
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
    noCache true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
