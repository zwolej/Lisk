buildscript {
    ext {
        gradleGitVersion = '1.7.2'
        gradleDockerVersion = '0.17.2'
        springBootVersion = '1.5.9.RELEASE'
        mariaDbVersion = '2.2.1'
        liquibaseVersion = '3.4.1'
        lombokVersion = '1.16.18'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:$gradleDockerVersion")
        classpath("org.ajoberstar:gradle-git:$gradleGitVersion")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.palantir.docker'

jar {
    baseName = 'backend'
    version =  '0.0.1'
}

repositories {
    mavenCentral()
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.mariadb.jdbc:mariadb-java-client:$mariaDbVersion")
    compile("org.liquibase:liquibase-core:$liquibaseVersion")

    compileOnly("org.projectlombok:lombok:$lombokVersion")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

docker {
    name "io.lisk/${jar.baseName}:${grgit.head().getAbbreviatedId()}"
    dockerfile file('Dockerfile')
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
    noCache true
}
